find_package(PkgConfig)
pkg_check_modules(PC_LIBZIP QUIET libzip)

find_path(LIBZIP_INCLUDE_DIR_ZIP
    NAMES zip.h
    HINTS ${PC_LIBZIP_INCLUDE_DIRS})

find_path(LIBZIP_INCLUDE_DIR_ZIPCONF
    NAMES zipconf.h
    HINTS ${PC_LIBZIP_INCLUDE_DIRS})

set(_LIBZIP_STATIC_LIBS libzip.a)
set(_LIBZIP_SHARED_LIBS zip)

if(USE_STATIC_LIBS)
  set(_LIBZIP_ORIG_SUFFIXES ${CMAKE_FIND_LIBRARY_SUFFIXES})
  # Prefer static builds but allow falling back to shared libs when static archives are missing
  set(CMAKE_FIND_LIBRARY_SUFFIXES ".a" ".so" ".dylib")
  find_library(LIBZIP_LIBRARY
      NAMES ${_LIBZIP_STATIC_LIBS} ${_LIBZIP_SHARED_LIBS}
      HINTS ${PC_LIBZIP_LIBRARY_DIRS})
  set(CMAKE_FIND_LIBRARY_SUFFIXES ${_LIBZIP_ORIG_SUFFIXES})
else()
  find_library(LIBZIP_LIBRARY
      NAMES ${_LIBZIP_SHARED_LIBS} ${_LIBZIP_STATIC_LIBS}
      HINTS ${PC_LIBZIP_LIBRARY_DIRS})
endif()

set(LIBZIP_INCLUDE_DIR ${LIBZIP_INCLUDE_DIR_ZIP})
set(LIBZIP_INCLUDE_DIRS ${LIBZIP_INCLUDE_DIR_ZIP} ${LIBZIP_INCLUDE_DIR_ZIPCONF})
list(REMOVE_DUPLICATES LIBZIP_INCLUDE_DIRS)
set(LIBZIP_LIBRARIES ${LIBZIP_LIBRARY})

include(FindPackageHandleStandardArgs)
FIND_PACKAGE_HANDLE_STANDARD_ARGS(
    LibZip DEFAULT_MSG
    LIBZIP_LIBRARY LIBZIP_INCLUDE_DIR_ZIP LIBZIP_INCLUDE_DIR_ZIPCONF)

mark_as_advanced(LIBZIP_LIBRARY LIBZIP_INCLUDE_DIR_ZIP LIBZIP_INCLUDE_DIR_ZIPCONF)

set(LIBZIP_VERSION 0)

if (LIBZIP_INCLUDE_DIR_ZIPCONF)
  FILE(READ "${LIBZIP_INCLUDE_DIR_ZIPCONF}/zipconf.h" _LIBZIP_VERSION_CONTENTS)
  if (_LIBZIP_VERSION_CONTENTS)
    STRING(REGEX REPLACE ".*#define LIBZIP_VERSION \"([0-9.]+)\".*" "\\1" LIBZIP_VERSION "${_LIBZIP_VERSION_CONTENTS}")
  endif ()
endif ()

set(LIBZIP_VERSION ${LIBZIP_VERSION} CACHE STRING "Version number of libzip")